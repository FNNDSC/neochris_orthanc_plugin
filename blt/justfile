# Build plugin and start Orthanc
up:
    cargo build && docker compose up

# Shut down and delete volumes
down:
    docker compose down -v

# Start a test run for the SAG-ANON dataset
test-run:
    xh :8042/blt/studies MRN=1449c1d Anon_PatientID=BCH01 PatientName=anonymized \
                         Anon_PatientName=BCH01 PatientBirthDate=01/07/2009 \
                         Search_AccessionNumber=98edede8b2 Anon_AccessionNumber=bch01-visit1 \
                         Anon_PatientBirthDate=19010101

# Rebuild plugin and restart Orthanc
restart:
    cargo build
    docker compose restart dev

# Delete all patients from Orthanc
reset:
    for api in patients jobs queries; do \
      xh :8042/$api | jaq -r '.[]' | xargs -I _ xh DELETE :8042/$api/_ ; \
    done

# Push a directory of DICOM files to PACS.
store dir:
    @just _upload_all /modalities/PACS/store-straight "{{dir}}"

# Upload a directory of DICOM files to DEV.
upload dir:
    @just _upload_all /instances "{{dir}}"

# Upload a directory of DICOM files to
_upload_all api_path dir:
    fd --type f --extension '.dcm' . '{{dir}}' \
        | rust-parallel --progress-bar --jobs 2 --discard-output=all just _upload_instance "{{api_path}}"

# Upload a DICOM file to the specified API endpoint.
_upload_instance api_path file:
    curl -sSfX POST -H 'Expect:' -H 'Content-Type: application/dicom' -T '{{file}}' -o /dev/null \
         "http://localhost:8042{{api_path}}"
